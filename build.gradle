buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21"
    }
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'java'
    apply plugin: "kotlin"

    sourceCompatibility = 11

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        implementation 'com.github.stefanbirkner:system-rules:1.19.0'
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        testCompile 'org.assertj:assertj-swing-junit:3.8.0'
        testImplementation 'com.github.hyperskill:hs-test:v7.0.1'
    }

    sourceSets {
        main.java.srcDir 'src'
        test.java.srcDir 'test'
    }

    test {
        systemProperty "file.encoding", "utf-8"
        outputs.upToDateWhen { false }
        afterTest { TestDescriptor test, TestResult result ->
            if (result.resultType == TestResult.ResultType.FAILURE) {
                def message = result.exception?.message ?: "Wrong answer"
                def lines = message.readLines()
                println "#educational_plugin FAILED + " + lines[0]
                lines[1..-1].forEach { line ->
                    println "#educational_plugin" + line
                }
            }
        }
    }

    compileJava.options.encoding = 'UTF-8'
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

wrapper {
    gradleVersion = '5.3.1'
}
